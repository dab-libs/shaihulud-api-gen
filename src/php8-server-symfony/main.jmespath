expression(
    'schemaToType',
    {"$ref": '', type: '', format: ''},
    &
    replace_all(@."$ref", '#/components/schemas/(\\w+)', '\\\\u$1') ||
    type == 'string' && format == 'date-time' && 'DateTimeImmutable' ||
    type == 'string' && 'string' ||
    type == 'integer' && 'int' ||
    type == 'number' && 'float' ||
    type == 'boolean' && 'bool' ||
    type == 'array' && 'array' ||
    null,
    @
).
expression(
    'schemaToTypeDoc',
    {"$ref": '', type: '', items: []},
    &
    replace_all(@."$ref", '#/components/schemas/(\\w+)', '\\\\u$1') ||
    type == 'string' && 'string' ||
    type == 'integer' && 'int' ||
    type == 'number' && 'float' ||
    type == 'boolean' && 'bool' ||
    type == 'array' && apply('schemaToTypeDoc', items) ||
    null,
    @
).
expression(
    'schemaToOperationsType',
    {"$ref": '', type: '', format: '', items: []},
    &
    apply('schemaToType', @) && concat( pascal_case( apply('schemaToType', @)), 'Operations') ||
    null,
    @
).
expression(
    'schemaToObjectOptions',
    {required: true, schema: {type: ''}},
    &
    concat(
      'required: ', required
    ),
    @
).
expression(
    'schemaToDateTimeImmutable',
    {required: true, schema: {type: ''}},
    &
    concat(
      'required: ', required
    ),
    @
).
expression(
    'schemaToString',
    {required: true, schema: {type: ''}},
    &
    concat(
      'required: ', required,
      ', minLength: ', schema.minLength,
      ', maxLength: ', schema.maxLength
    ),
    @
).
expression(
    'schemaToNumber',
    {required: true, schema: {type: ''}},
    &
    concat(
      'required: ', required,
      ', maximum: ', schema.maximum,
      ', exclusiveMaximum: ', schema.exclusiveMaximum,
      ', minimum: ', schema.minimum,
      ', exclusiveMinimum: ', schema.exclusiveMinimum,
      ', multipleOf: ', schema.multipleOf
    ),
    @
).
expression(
    'schemaToBool',
    {required: true, schema: {type: ''}},
    &
    concat(
      'required: ', required
    ),
    @
).
expression(
    'schemaToArray',
    {required: true, schema: {type: ''}},
    &
    concat(
      'required: ', required,
      ', minItems: ', schema.minItems,
      ', maxItems: ', schema.maxItems,
      ', validateItem: fn(mixed $decoded, string $name) => ', apply('schemaToOperationsType', schema.items),
      '::validate(decoded: $decoded, name: $name, ', apply('schemaToTypeOptions', {required: true, schema: schema.items})
    ),
    @
).
expression(
    'schemaToTypeOptions',
    {required: true, schema: {type: ''}},
    &
    contains(keys(schema), '$ref') && apply('schemaToObjectOptions', @) ||
    type == 'string' && format == 'date-time' && apply('schemaToDateTimeImmutable', @) ||
    type == 'string' && apply('schemaToString', @) ||
    type == 'integer' && apply('schemaToNumber', @) ||
    type == 'number' && apply('schemaToNumber', @) ||
    type == 'boolean' && apply('schemaToBool', @) ||
    type == 'array' && apply('schemaToArray', @) ||
    null,
    @
).
expression(
    'schemaIsPrimitive',
    {type: ''},
    &
    type == 'string' ||
    type == 'integer' ||
    type == 'number' ||
    type == 'boolean',
    @
).
expression(
    'createTypeInfo',
    {name: '', scheme: '', parent: ''},
    &
    {
        primitive: apply('schemaIsPrimitive', schema),
        type: apply('schemaToType', schema) ||
              halt(concat('Can not be generated code for "', name, '" in "', parent, '"')),
        operationsType: apply('schemaToOperationsType', schema),
        arrayItem: schema.type == 'array' && apply('createTypeInfo', {
            schema: schema.items,
            name: concat(name, '.item'),
            parent: concat(name, '.', parent)
        })
    },
    @
).
merge(@, {
    vendor: kebab_case(info.contact.name),
    majorVersion: set_var('majorVersion', replace_all(info.version, '(\\d+)\\.(\\d+)\\.(\\d+)', '$1')),
    minorVersion: replace_all(info.version, '(\\d+)\\.(\\d+)\\.(\\d+)', '$2'),
    apiClass: pascal_case(info.title),
    apiName: set_var('apiName', pascal_case(info.title)),
    rootFolder: set_var('rootFolder', './'),
    vendorNamespace: set_var('vendorNamespace', pascal_case(info.contact.name)),
    package: concat(kebab_case(info.title), '-server-v', get_var('majorVersion')),
    bundleFolder: concat(get_var('rootFolder'), 'bundle/'),
    bundleBase: set_var('bundleBase', concat(get_var('apiName'), 'V', get_var('majorVersion'))),
    bundleName: set_var('bundleName', concat(get_var('bundleBase'), 'Bundle')),
    bundleNamespace: concat(get_var('vendorNamespace'), '\\', get_var('bundleName')),
    bundleExtension: concat(get_var('bundleBase'), 'Extension'),
    srcFolder: set_var('srcFolder', concat(get_var('rootFolder'), 'src/')),
    namespace: set_var('namespace', concat(get_var('vendorNamespace'), '\\', get_var('apiName'), '\\v', get_var('majorVersion'))),
    publicFolder: concat(get_var('srcFolder'), 'Public/'),
    publicNamespace: concat(get_var('namespace'), '\\Public'),
    controllerFolder: concat(get_var('srcFolder'), 'Controller/'),
    controllerNamespace: concat(get_var('namespace'), '\\Controller'),
    typeOperationFolder: concat(get_var('srcFolder'), 'TypeOperation/'),
    typeOperationNamespace: concat(get_var('namespace'), '\\TypeOperation'),
    configFolder: concat(get_var('rootFolder'), 'resource/config/'),
    testFolder: concat(get_var('rootFolder'), 'test/'),
    testNamespace: concat(get_var('namespace'), '\\Test'),

    apiKeysInHeader: values(components.securitySchemes)[?type == 'apiKey' && in == 'header'].name,
    apiKeysInQuery: values(components.securitySchemes)[?type == 'apiKey' && in == 'query'].name,
    objects: entries(components.schemas, 'schemaName', 'schema')[].{
        type: set_var('objectType', upper_case_first(schemaName)),
        scheme: schema,
        description: schema.description,
        properties: set_var('properties', set_var('required', if(type(schema.required) == 'array', @, &schema.required, &empty_array()), sort_by(entries(schema.properties, 'propertyName', 'propertySchema'), &propertyName))[].{
            description: propertySchema.description,
            name: propertyName,
            schema: propertySchema,
            nullableDoc: if(contains(get_var('required'), propertyName), @, &'', &' | null'),
            nullable: if(contains(get_var('required'), propertyName), @, &'', &'?'),
            typeInfo: apply('createTypeInfo', {
                schema: propertySchema,
                name: propertyName,
                parent: get_var('objectType')
            })
        }),
        importedTypes: create_set([
            get_var('properties')[?typeInfo.type == 'DateTimeImmutable'].typeInfo.type
        ][])[*]
    },
    controllers: entries(paths, 'route', 'requestsByMethod')[].{
        route: set_var('route', route),
        requests: entries(requestsByMethod, 'method', 'request')[].{
            route: get_var('route'),
            method: method,
            request: request,
            controllerType: set_var('controllerType', concat(set_var('operationId', pascal_case(request.operationId)), 'Controller')),
            testType: set_var('testType', concat(get_var('operationId'), 'ControllerTests')),
            actionType: concat(get_var('operationId'), 'Action'),
            actionMethod: camel_case(request.operationId),
            parameters: set_var('parameters', request.parameters[].{
                name: name,
                path: in == 'path',
                query: in == 'query',
                header: in == 'header',
                cookie: in == 'cookie',
                in: in,
                required: required,
                schema: schema,
                typeInfo: apply('createTypeInfo', {
                    schema: schema,
                    name: name,
                    parent: get_var('controllerType')
                }),
                typeOptions: apply('schemaToTypeOptions', {required: required, schema: schema}),
                bagName: property({path: 'attributes', query: 'query', header: 'headers', cookie: 'cookies'}, in),
                bagMethod: if(schema.type == 'array', @, &'all', &'get')
            }),
            body: set_var('body', if(request.requestBody.content."application/json".schema, @, &{
                schema: request.requestBody.content."application/json".schema,
                typeInfo: apply('createTypeInfo', {
                    schema: request.requestBody.content."application/json".schema,
                    name: 'body',
                    parent: get_var('controllerType')
                })
            })),
            results: set_var('results', entries(request.responses, 'statusCode', 'response')[].{
                statusCode: statusCode,
                type: concat(get_var('operationId'), 'Result', upper_case_first(statusCode)),
                content: apply('createTypeInfo', {
                    schema: response.content."application/json".schema,
                    name: 'result.content',
                    parent: get_var('operationId')
                }),
                headersType: if(response.headers, @, &concat(get_var('operationId'), 'Headers', upper_case_first(statusCode))),
                headers: response.headers.if(@, @, &entries(@, 'name', 'header')[].{
                    name: name,
                    fieldName: camel_case(name),
                    schema: schema,
                    typeInfo: apply('createTypeInfo', {
                        schema: schema,
                        name: name,
                        parent: get_var('controllerType')
                    })
                })
            }),
            importedTypes: create_set([
                get_var('parameters')[?!typeInfo.primitive].typeInfo.type,
                get_var('parameters')[?!typeInfo.arrayItem.primitive].typeInfo.arrayItem.type,
                get_var('results')[?!content.primitive].content.type,
                get_var('results')[?!content.arrayItem.primitive].content.arrayItem.type
            ][])[*],
            importedOperationsTypes: create_set([
                get_var('parameters')[].typeInfo.operationsType,
                get_var('parameters')[].typeInfo.arrayItem.operationsType,
                get_var('body')[].typeInfo.operationsType,
                get_var('body')[].typeInfo.arrayItem.operationsType,
                get_var('results')[].content.operationsType,
                get_var('results')[].content.arrayItem.operationsType
            ][])[*]
        }
    }|[].requests[]
})